PREFIX ex: <http://ex.org/a#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?MATCH_IRI a ex:Match;
    ex:tourney ?TORNEY_IRI;
    ex:match_num ?MATCH_NUM;
    ex:winner ?WINNER_IRI;
    ex:loser ?LOSER_IRI;
    ex:score ?score;
    ex:best_of ?BEST_OF;
    ex:round ?ROUND;
    ex:winner_age ?WINNER_AGE;
    ex:winner_rank ?WINNER_RANK;
    ex:winner_rank_points ?WINNER_RANK_POINTS;
    ex:loser_age ?LOSER_AGE;
    ex:loser_rank ?LOSER_RANK;
    ex:loser_rank_points ?LOSER_RANK_POINTS;
  .
}
WHERE {
  BIND (IRI(CONCAT('http://ex.org/a#', 'Match-', ?tourney_id, '-', ?match_num)) AS ?MATCH_IRI)
  BIND (IRI(CONCAT('http://ex.org/a#', 'Tournament-', ?tourney_id)) AS ?TORNEY_IRI)
  BIND (IRI(CONCAT('http://ex.org/a#', replace(?winner_name, " ", "-"))) AS ?WINNER_IRI)
  BIND (IRI(CONCAT('http://ex.org/a#', replace(?loser_name, " ", "-"))) AS ?LOSER_IRI)
  BIND (xsd:integer(?match_num) AS ?MATCH_NUM)
  BIND (xsd:integer(?best_of) AS ?BEST_OF)
  BIND (xsd:integer(?round) AS ?ROUND)
  BIND (xsd:integer(?minutes) AS ?MINUTES)
  BIND (xsd:float(?winner_age) AS ?WINNER_AGE)
  BIND (xsd:integer(?winner_rank) AS ?WINNER_RANK)
  BIND (xsd:integer(?winner_rank_points) AS ?WINNER_RANK_POINTS)
  BIND (xsd:float(?loser_age) AS ?LOSER_AGE)
  BIND (xsd:integer(?loser_rank) AS ?LOSER_RANK)
  BIND (xsd:integer(?loser_rank_points) AS ?LOSER_RANK_POINTS)
}

CONSTRUCT {
  ?TOURNEY_IRI a ex:Tournament ;
    ex:name ?tourney_name;
    ex:surface ?surface;
    ex:draw_size ?DRAW_SIZE;
    ex:tourney_level ?tourney_level;
    ex:tourney_date ?TOURNEY_DATE;
  .
}
WHERE {
  BIND (IRI(CONCAT('http://ex.org/a#', 'Tournament-', ?tourney_id)) AS ?TOURNEY_IRI)
  BIND (xsd:integer(?draw_size) AS ?DRAW_SIZE)
  BIND (xsd:string(?tourney_date) AS ?tourney_date_str)
  BIND (xsd:string(SUBSTR(?tourney_date_str, 1, 4)) AS ?tourney_year)
  BIND (xsd:string(SUBSTR(?tourney_date_str, 5, 2)) AS ?tourney_month)
  BIND (xsd:string(SUBSTR(?tourney_date_str, 7, 2)) AS ?tourney_day)
  BIND (xsd:date(CONCAT(?tourney_year,"-", ?tourney_month,"-", ?tourney_day)) AS ?TOURNEY_DATE)
}

CONSTRUCT {
  ?WINNER_IRI a ex:Player ;
    ex:name ?winner_name ;
    ex:hand ?winner_hand ;
    ex:height ?WINNER_HEIGHT ;
    ex:country ?winner_ioc ;
  .
}
WHERE {
  BIND (IRI(CONCAT('http://ex.org/a#', replace(?winner_name, " ", "-"))) AS ?WINNER_IRI)
  BIND (xsd:integer(?winner_ht) AS ?WINNER_HEIGHT)
  FILTER(?winner_hand != "U" && BOUND(?winner_ht))
}

CONSTRUCT {
  ?LOSER_IRI a ex:Player ;
    ex:name ?loser_name ;
    ex:hand ?loser_hand ;
    ex:height ?LOSER_HEIGHT ;
    ex:country ?loser_ioc ;
  .
}
WHERE {
  BIND (IRI(CONCAT('http://ex.org/a#', replace(?loser_name, " ", "-"))) AS ?LOSER_IRI)
  BIND (xsd:integer(?loser_ht) AS ?LOSER_HEIGHT)
  FILTER (?loser_hand != "U" && BOUND(?loser_ht))
}